buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6'
    }
}


apply plugin: 'java'

version = '0.1.1'

repositories {
    jcenter()
}

dependencies {
    testCompile 'junit:junit:4.11'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'src'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'doc'
    from javadoc.destinationDir
}


apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

artifacts {
    archives jar, sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.nelsoncrosby'

            from components.java

            artifact sourcesJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
}

bintray {
    // Define these in gradle.properties for distribution
    user = hasProperty('bintrayUser') ? bintrayUser : ''
    key = hasProperty('bintrayKey') ? bintrayKey : ''

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'NCUtils'
        desc = ' A small collection of everyday Java utilities'
        licenses = ['MIT']

        websiteUrl = 'https://github.com/NelsonCrosby/NCUtils'
        issueTrackerUrl = 'https://github.com/NelsonCrosby/NCUtils/issues'
        vcsUrl = 'https://github.com/NelsonCrosby/NCUtils.git'
    }
}
